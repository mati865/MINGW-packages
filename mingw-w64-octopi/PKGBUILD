# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=octopi
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
#_qmake=${MINGW_PREFIX}/bin/qmake
_qmake=${MINGW_PREFIX}/qt5-static/bin/qmake
pkgver=0.8.1
pkgrel=1
pkgdesc="a powerful Pacman frontend using Qt libs"
arch=('any')
url="https://github.com/aarnt/octopi"
license=('GPL2')
makedepends=("${MINGW_PACKAGE_PREFIX}-qt5-static" "${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
#options=('debug' '!strip')
source=("https://github.com/aarnt/${_realname}/archive/v${pkgver}.tar.gz"
        "0001-Use-Q_PID-instead-of-int-reducing-QProcess-repititio.patch"
        "0002-Use-a-shared-QProcessEnvironment.patch"
        "0003-Win32-Link-to-Windows-Terminal-Services-wtsapi32.patch"
        "0004-Remove-redundancy-in-UnixCommand-s-exe-checking-func.patch"
        "0005-Use-instead-of-QDir-separator.patch"
        "0006-Append-to-instead-of-overwrite-m_readAllStandardOutp.patch"
        "0007-Define-QT_WA.patch"
        "0008-MSYS2-Add-ectn_MSYS2-as-a-LinuxDistro.patch"
        "0009-MSYS2-Add-getMSYS2Root.patch"
        "0010-MSYS2-hasPacmanDatabase-getMSYS2Root-changes.patch"
        "0011-MSYS2-Changes-to-various-system-calls.patch"
        "0012-MSYS2-Process-id-changes-WIP.patch"
        "0013-MSYS2-Prepend-getMSYS2Root-to-file-checks.patch"
        "0014-MSYS2-Support-discoverBinaryPath.patch")
sha256sums=('e01c6d959e5663797771b95b47951e3ad11a9abbc7cbb009f1f835404c5ff43e'
            'd23e101357d53f7705768d88474e936e139c8493ac20eb655768eba8ce2b974f'
            '81f779e3dc6ecb8e4661ae8d2d5b6a6e76ffe1c46ee73a8fccfe36e90a70fd46'
            'a8896770e3c5a0e5219e81caebf172a0f1b0694f0ea1c0d00389fc17154af710'
            '7ea63a8c4265f154634f23297e797981f44b5d5618eb3efcd4d30fc0d6d4a915'
            'dcddf00d670aa4a345a1e4f4aae36a179df84161b31fb3164a0781ac4d42c843'
            '34030b89d0fe3165dfc6ba5a578621cc37545e6ac29bf292b702065b3bdbd9a5'
            '1421f68f11a9ed86bf202bba6ec7a15a0f29f24e2b7e15913ba1b9e453c30938'
            'cbb7f0847ddf2d37864beefba51306823afce2b84b5c18df7d9edf26082c3468'
            '200e52340ef7131908434a17d8c2ac73b43c096fd3ee490e7e806424053ca4e3'
            '3b65ecaf3f82d2604b12b7713d07cec27b86112203fb0334ee730ffae771696c'
            'bcf26c6d557c63bd2a91441b4b9d40e21b26f511ba0326502d8b629ee7a95c69'
            '7dd364cc765a2d4698a6219505642c02853e5c29a6b350e70bfb8d0089f8d895'
            '073bf9be60218e56b3b2ea3c812cc729213351bda8fdb3455d286a7c68448e39'
            'ba24d9718f4ab1318f3f3318270118d9b9505a362e6291017e4b76baf0f759f5')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i "${srcdir}"/0001-Use-Q_PID-instead-of-int-reducing-QProcess-repititio.patch
  patch -p1 -i "${srcdir}"/0002-Use-a-shared-QProcessEnvironment.patch
  patch -p1 -i "${srcdir}"/0003-Win32-Link-to-Windows-Terminal-Services-wtsapi32.patch
  patch -p1 -i "${srcdir}"/0004-Remove-redundancy-in-UnixCommand-s-exe-checking-func.patch
  patch -p1 -i "${srcdir}"/0005-Use-instead-of-QDir-separator.patch
  patch -p1 -i "${srcdir}"/0006-Append-to-instead-of-overwrite-m_readAllStandardOutp.patch
  patch -p1 -i "${srcdir}"/0007-Define-QT_WA.patch
  patch -p1 -i "${srcdir}"/0008-MSYS2-Add-ectn_MSYS2-as-a-LinuxDistro.patch
  patch -p1 -i "${srcdir}"/0009-MSYS2-Add-getMSYS2Root.patch
  patch -p1 -i "${srcdir}"/0010-MSYS2-hasPacmanDatabase-getMSYS2Root-changes.patch
  patch -p1 -i "${srcdir}"/0011-MSYS2-Changes-to-various-system-calls.patch
  patch -p1 -i "${srcdir}"/0012-MSYS2-Process-id-changes-WIP.patch
  patch -p1 -i "${srcdir}"/0013-MSYS2-Prepend-getMSYS2Root-to-file-checks.patch
  patch -p1 -i "${srcdir}"/0014-MSYS2-Support-discoverBinaryPath.patch
}

build() {
  local _config
  cd ${srcdir}/
  if check_option "debug" "n"; then
    _config="release"
  else
    _config="debug"
  fi

  rm -rf build-${MINGW_CHOST}
  mkdir build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  msg "Building Octopi..."
  ${_qmake} ../${_realname}-${pkgver}/${_realname}.pro CONFIG+="c++11 ${_config}"
  make ${_config}

  mkdir -p notifier/pacmanhelper
  pushd notifier/pacmanhelper
  msg "Building pacmanhelper..."
  ${_qmake} ../../../${_realname}-${pkgver}/notifier/pacmanhelper/pacmanhelper.pro CONFIG+="${_config}"
  make ${_config}
  popd

  mkdir -p notifier/${_realname}-notifier
  pushd notifier/${_realname}-notifier
  msg "Building ${_realname}-notifier..."
  ${_qmake} ../../../${_realname}-${pkgver}/notifier/${_realname}-notifier/${_realname}-notifier.pro CONFIG+="release"
  make ${_config}
  popd
}

package() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}

  cd "${srcdir}/build-${MINGW_CHOST}"

  install -D -m755 bin/${_realname}                                            ${pkgdir}${MINGW_PREFIX}/bin/${_realname}
  install -D -m755 notifier/bin/${_realname}-notifier                          ${pkgdir}${MINGW_PREFIX}/bin/${_realname}-notifier
  install -D -m644 ${srcdir}/${_realname}-${pkgver}/resources/images/${_realname}_red.png ${pkgdir}${MINGW_PREFIX}/share/icons/$}_realname}.png

  #services
  install -D -m755 notifier/bin/pacmanhelper                                  ${pkgdir}${MINGW_PREFIX}/lib/octopi/pacmanhelper

  install -D -m644 ${srcdir}/${_realname}-${pkgver}/notifier/pacmanhelper/polkit/org.octopi.pacman.policy        ${pkgdir}${MINGW_PREFIX}/share/polkit-1/actions/org.octopi.pacman.policy
  install -D -m644 ${srcdir}/${_realname}-${pkgver}/notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.conf    ${pkgdir}${MINGW_PREFIX}/etc/dbus-1/system.d/org.octopi.pacmanhelper.conf
  install -D -m644 ${srcdir}/${_realname}-${pkgver}/notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.xml     ${pkgdir}${MINGW_PREFIX}/share/dbus-1/interfaces/org.octopi.pacmanhelper.xml
  install -D -m644 ${srcdir}/${_realname}-${pkgver}/notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.service ${pkgdir}${MINGW_PREFIX}/share/dbus-1/system-services/org.octopi.pacmanhelper.service
}

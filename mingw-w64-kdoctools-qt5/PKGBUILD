# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>
# Contributor (MSYS2): Mateusz Miku≈Ça <mati865@gmail.com>

_variant=-${KF5_VARIANT:-shared}
source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks5
_kde_f5_init_package "${_variant}" "kdoctools"
pkgver=5.24.0
pkgrel=1
arch=('any')
pkgdesc="Create documentation from DocBook (mingw-w64-qt5${_namesuff})"
license=('LGPL')
options=('staticlibs')
makedepends=("${MINGW_PACKAGE_PREFIX}-extra-cmake-modules"
             "perl"
             "perl-URI")
depends=()
_kde_f5_add_depends "${MINGW_PACKAGE_PREFIX}-qt5${_namesuff}" "${MINGW_PACKAGE_PREFIX}-docbook-xml" "${MINGW_PACKAGE_PREFIX}-docbook-xsl"
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${_basename}-${pkgver}.tar.xz"
        "hack-fix-paths.patch")
sha256sums=('3193cf3438bf6b7954c8e3057d35e4cc97ea0c550bb383685a213480ef4c22e4'
            '8b8cacdaa94a075a74af9889c73974e3a6fb0115c5e52b39e892f1c70cb17fe8')

prepare() {
  mkdir -p build-${CARCH}${_variant}
  
  cd ${srcdir}/${_basename}-${pkgver}
  patch -p1 -i ${srcdir}/hack-fix-paths.patch
}

build() {
  local -a extra_config
  cd build-${CARCH}${_variant}
  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
    QT5_PREFIX=${MINGW_PREFIX}/qt5-static
    export PATH=${QT5_PREFIX}/bin:"$PATH"
  else
    QT5_PREFIX=${MINGW_PREFIX}
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;-DECM_MKSPECS_INSTALL_DIR=" \
    cmake ../${_basename}-${pkgver} \
      -DCMAKE_BUILD_TYPE=$(_kde_f5_CMAKE_BUILD_TYPE) \
      -DCMAKE_INSTALL_PREFIX=${QT5_PREFIX} \
      -DKDE_INSTALL_LIBDIR=lib \
      -DECM_MKSPECS_INSTALL_DIR=${QT5_PREFIX}/share/qt5/mkspecs \
      -DBUILD_TESTING=OFF \
      -DECM_DIR=${MINGW_PREFIX}/share/ECM \
      "${extra_config[@]}" \
      -G'MSYS Makefiles'
  make
}

package() {
  cd build-${CARCH}${_variant}
  make DESTDIR="${pkgdir}" install
}

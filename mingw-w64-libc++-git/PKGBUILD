# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libc++
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.0.0
pkgrel=1
pkgdesc='A new implementation of the C++ standard library, targeting C++11.'
url='http://libcxx.llvm.org'
license=('custom:University of Illinois/NCSA Open Source License')
arch=('any')
# gcc-libs needed for libgcc_s, because mingw-w64 doesn't use a pure compiler-rt clang
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
        #"${MINGW_PACKAGE_PREFIX}-libc++abi"
        )
makedepends=("${MINGW_PACKAGE_PREFIX}-clang"
             "${MINGW_PACKAGE_PREFIX}-cmake")
source=("libcxx"::"git+http://llvm.org/git/libcxx.git"
        "libcxxabi"::"git+http://llvm.org/git/libcxxabi.git"
        "llvm"::"git+http://llvm.org/git/llvm.git"
        "0401-mingw-w64-hack-and-slash-fixes-for-libc.patch"
        "0701-mingw-hacks.patch")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            '70fd7f454618b8d2ea9e7490f99a78eec0e28e57e91eeb91b12344d583606072'
            '45023482cdb4e61a36c8b928c4f013ca8776fb3f9700e100cb2ad0093b454365')

prepare() {
  cd "${srcdir}/libcxx"
  gitam_mkpkg "${srcdir}/0401-mingw-w64-hack-and-slash-fixes-for-libc.patch"

  cd "${srcdir}/libcxxabi"
  patch -p1 -i "${srcdir}/0701-mingw-hacks.patch"

  cd "${srcdir}"/llvm
  rm -rf projects/libcxx projects/libcxxabi | true
  mv "${srcdir}/libcxxabi"         projects/libcxxabi
  mv "${srcdir}/libcxx"            projects/libcxx
}

build() {
  # [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  # export CXX="${MINGW_PREFIX}/bin/clang++"
  # export CC="${MINGW_PREFIX}/bin/clang"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="-D_GNU_SOURCE" \
    -DLIBCXX_CXX_ABI=libsupc++ \
    -DLIBCXX_CXX_ABI_INCLUDE_PATHS="${MINGW_PREFIX}/include/c++/6.3/;${MINGW_PREFIX}/include/c++/6.3/${MINGW_CHOST}/" \
    -DLIBCXX_ENABLE_SHARED=OFF \
    -DLIBCXX_ENABLE_STATIC=ON \
    -DLIBCXX_ENABLE_EXPERIMENTAL_LIBRARY=ON \
    -DLIBCXX_ENABLE_FILESYSTEM=OFF \
    -DLIBCXX_HAS_PTHREAD_API=ON \
    -DLIBCXX_USE_COMPILER_RT=OFF \
    -DLIBCXXABI_ENABLE_SHARED=OFF \
    -DLIBCXXABI_ENABLE_STATIC=ON \
    -DLIBCXXABI_HAS_PTHREAD_API=ON \
    ../llvm
    #-DLIBCXX_USE_COMPILER_RT=ON \
    # -DLIBCXX_CXX_ABI=libstdc++ \
    # -DLIBCXX_CXX_ABI_INCLUDE_PATHS="${MINGW_PREFIX}/include/c++/6.3/;${MINGW_PREFIX}/include/c++/6.3/${MINGW_CHOST}/" \

  make cxx cxx_experimental cxxabi
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install-libcxx
  make DESTDIR="$pkgdir" install-libcxxabi
}

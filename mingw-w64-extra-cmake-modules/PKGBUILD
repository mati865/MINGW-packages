# $Id$
# Maintainer (ArchLinux): Andrea Scarpino <andrea@archlinux.org>
# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>

_realname=extra-cmake-modules
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.20.0
pkgrel=1
pkgdesc='Extra CMake modules (mingw-w64)'
arch=('any')
url='https://projects.kde.org/projects/kdesupport/extra-cmake-modules'
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-cmake"
         "${MINGW_PACKAGE_PREFIX}-png2ico")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-sphinx") # qt5-tools for QtHelp pages
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${_realname}-${pkgver}.tar.xz"
        "set-AUTOSTATICPLUGINS.patch"
        "use-UNIX-paths-for-MSYS.patch"
        fix-ECMGeneratePriFile.cmake.patch::"https://quickgit.kde.org/?p=extra-cmake-modules.git&a=commitdiff&h=5bb92992&o=plain")
sha256sums=('8a4fd5eac37d4c6a4998c48716efe2392c6f7ec9a124aab4c8fc26516815a106'
            '30bdcedab402c69ea0db3460f5a23cbd226a5cd1e12b13926b8a65df773e14a0'
            'a1617a9a462bee107ddab6b168ac95dd7a256d2dce153c018bcb0b5947dde3d0'
            '35c5945d63fb5d5c0f4c1c98b9796e9e6639ba2642defbd81b19307a0612a47b')

prepare() {
  mkdir -p build-${CARCH}

  # Apply patches
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/set-AUTOSTATICPLUGINS.patch
  patch -p1 -i "${srcdir}"/use-UNIX-paths-for-MSYS.patch
  patch -p1 -i "${srcdir}"/fix-ECMGeneratePriFile.cmake.patch
}

build() {
  mkdir -p build-${CARCH}
  cd build-${CARCH}
  MSYS2_ARG_CONV_EXCL=-DCMAKE_INSTALL_PREFIX= \
    cmake ../${_realname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_HTML_DOCS=OFF \
      -G'MSYS Makefiles'
  make
}

package() {
  cd build-${CARCH}
  make DESTDIR="${pkgdir}" install
}
